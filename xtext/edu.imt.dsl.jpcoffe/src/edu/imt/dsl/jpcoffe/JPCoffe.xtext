grammar edu.imt.dsl.jpcoffe.JPCoffe hidden(WS, ML_COMMENT, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate jPCoffe "http://www.imt.edu/dsl/jpcoffe/JPCoffe"

Main:
	(imports += Import)*
	(recipes += Recipe)+
;

Import :
	"use" "recipe" NAME "in" STRING
;

Recipe : 
	"Recipe" name = STRING '{'
		portion = PortionNB
		ingredients = IngredientsBlock
		tools = ToolsBlock
		steps = StepsBlock
	'}'
;

PortionNB : 
	"For" nb = INT ("people")?
;

IngredientsBlock :
	{IngredientsBlock} "Ingredients"
	TITLE_LINE
	(ingredientsList += (IngredientsGroup | Ingredient))*
;

IngredientsGroup :
	'*' "GROUP" name = STRING '[' 
		(ingredientsList += Ingredient)*
	']'
	(quantity = Quantity)?
;

Ingredient :
	'*' 
	(originalName += NAME ("OR" originalName += NAME)* "AS")?
	name = NAME
	(quantity = Quantity)?
;

Quantity :
	amount = INT (unit = METRIC_UNIT)?
;

terminal METRIC_UNIT :
	"gr" | "kg" | "cl" | "l" | "g"
;

ToolsBlock :
	{ToolsBlock} "Tools"
	TITLE_LINE
	(toolsList += Tool)*
;

Tool :
	'*'
	(originalName = NAME "AS")? name = NAME
;

StepsBlock : 
	{StepsBlock} "Steps"
	TITLE_LINE
	(stepsList += (ConditionalStep | Step))*
;

ConditionalStep :
	"{" (pred += INT)? ("," pred += INT)* "}" 
	"->" num = INT "."
	"IF" condition = TEXT
	"THEN" to = INT
;

Step :
	"{" (pred += INT)? ("," pred += INT)* "}" 
	"->" num = INT "."
	text = TEXT
;

NAME : MAJ_WORD (WORD)*;
TEXT : (MAJ_WORD | WORD | INT)+;

terminal TITLE_LINE : ("-")+;
terminal MAJ_WORD : ('A'..'Z') ('a'..'z' | '-')*;
terminal WORD : ('a'..'z'|'A'..'Z'|'\''|','|'-')+ ;

// from org.eclipse.xtext.common.Terminals

terminal INT returns ecore::EInt: ('0'..'9')+;
terminal STRING:
	'"' ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|'"') )* '"' |
	"'" ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|"'") )* "'"
;
terminal ML_COMMENT : '/*' -> '*/';
terminal SL_COMMENT : '//' !('\n'|'\r')* ('\r'? '\n')?;

terminal WS : (' '|'\t'|'\r'|'\n')+;
